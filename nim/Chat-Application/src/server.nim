import asyncdispatch, asyncnet

type
    Client = ref object
        socket: AsyncSocket
        netAddr: string
        id: int
        connected: bool
    Server = ref object
        socket: AsyncSocket
        clients: seq[Client]

proc newServer(): Server = Server(socket: newAsyncSocket(), clients: @[])
var server = newServer()

proc `$`(client: Client): string = 
    $client.id & "(" & client.netAddr & ")"

proc processMessages(server: Server, client: Client) {.async.} =
    while true:
        let line = await client.socket.recvLine()
        if line.len == 0:
            echo(client, " disconnected!")
            client.connected = false
            client.socket.close()
            return
        echo(client, " sent: ", line)
        for c in server.clients:
            if c.id != client.id and c.connected:
                await c.socket.send(line & "\c\1")

proc loop(server: Server, port = 7688) {.async.} =
    server.socket.bindAddr(port.Port)
    server.socket.listen()

    while true:
        let (netAddr, clientSocket) = await server.socket.acceptAddr()
        echo ("Accepted connection from", netAddr)
        let client = Client(
            socket: clientSocket,
            netAddr: netAddr,
            id: server.clients.len,
            connected: true
        )
        server.clients.add(client)
        asyncCheck processMessages(server, client)
waitFor loop(server)
